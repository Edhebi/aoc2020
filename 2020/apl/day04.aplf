∇ day04
    {
        input ← ⊃⎕NGET (InputFile 4) 1
        infos ← {⊃,/(≠∘' '⊆⊢)¨⍵}¨(×∘≢¨⊆⊢)input
        cards ← {⍉↑(≠∘':'⊆⊢)¨⍵}¨infos
        req ← 'byr' 'iyr' 'eyr' 'hgt' 'hcl' 'ecl' 'pid'
        check1 ← {req =⍥≢ req∩1⌷⍵}
        checkF ← {
            reg ← {
                'byr'≡⍵: '^19[2-9][0-9]|200[0-2]$'
                'iyr'≡⍵: '^20(1[0-9]|20)$'
                'eyr'≡⍵: '^20(2[0-9]|30)$'
                'hgt'≡⍵: '^(1([5-8][0-9]|9[0-3]))cm|(59|6[0-9]|7[0-6])in$'
                'hcl'≡⍵: '^#[a-z0-9]{6}$'
                'ecl'≡⍵: '^amb|blu|brn|gry|grn|hzl|oth$'
                'pid'≡⍵: '^[0-9]{9}$'
                '^.*$'
            } ⍺
            ×≢⊃(reg⎕S'\0')⍵
        }
        check2 ← check1 ∧ (∧/1∘⌷ checkF¨ 2∘⌷)
        ⎕← 'Part 1:', +/check1¨cards
        ⎕← 'Part 2:', +/check2¨cards
    } ⍬
∇